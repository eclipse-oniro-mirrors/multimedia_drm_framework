# Copyright (C) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("//foundation/multimedia/drm_framework/config.gni")

subsystem_name = "multimedia"
part_name = "drm_framework"
taihe_generated_file_path = "$taihe_file_path/out/$subsystem_name/$part_name"


copy_taihe_idl("copy_drm_taihe") {
  sources = [
    "//foundation/multimedia/drm_framework/frameworks/taihe/idl/ohos.multimedia.drm.taihe",
  ]
}

ohos_taihe("run_drm_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  deps = [ ":copy_drm_taihe" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.multimedia.drm.ani.cpp",
    "$taihe_generated_file_path/src/ohos.multimedia.drm.abi.c",
  ]
}

config("drm_taihe_config") {
  visibility = [ ":*" ]
  include_dirs = [ "//foundation/multimedia/drm_framework/frameworks/taihe/include",]
}

taihe_shared_library("drm_taihe") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    cfi_vcall_icall_only = true
    debug = false
  }

  taihe_generated_file_path = "$taihe_generated_file_path"
  subsystem_name = "$subsystem_name"
  part_name = "$part_name"

  public_configs = [ ":drm_taihe_config" ]

  include_dirs = [
    "//foundation/multimedia/drm_framework/frameworks/taihe/include",
    "//foundation/multimedia/drm_framework/frameworks/native/drm",
    "//foundation/multimedia/drm_framework/frameworks/native/common",
    "//foundation/multimedia/drm_framework/interfaces/inner_api/native/drm",
    "//foundation/multimedia/drm_framework/services/drm_service/",
    "//foundation/multimedia/drm_framework/services/drm_service/client/include",
    "//foundation/multimedia/drm_framework/services/drm_service/server/include",
    "//foundation/multimedia/drm_framework/services/drm_service/ipc",
    "//foundation/multimedia/drm_framework/services/utils/include",
  ]

  sources = get_target_outputs(":run_drm_taihe")
  sources += [
    "src/ani_constructor.cpp",
    "src/media_key_system_taihe.cpp",
    "src/key_session_taihe.cpp",
    "src/drm_taihe_utils.cpp",
    "src/key_session_callback_taihe.cpp",
    "src/media_key_system_callback_taihe.cpp",
    "./../../services/utils/drm_api_operation.cpp",
  ]

  deps = [
    ":run_drm_taihe",
    "${drm_framework_root_dir}/frameworks/native:drm_framework",
  ]
  external_deps = [
    "ability_runtime:abilitykit_native",
    "c_utils:utils",
    "hiappevent:hiappevent_innerapi",
    "hilog:libhilog",
    "hisysevent:libhisysevent",
    "hitrace:libhitracechain",
    "ipc:ipc_core",
    "eventhandler:libeventhandler",
  ]
}

generate_static_abc("drm_framework_taihe_abc") {
  base_url = "$taihe_generated_file_path"
  files = [ "$taihe_generated_file_path/@ohos.multimedia.drm.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/drm_framework_taihe_abc.abc"
  dependencies = [ ":run_drm_taihe" ]
}

ohos_prebuilt_etc("drm_framework_etc") {
  source = "$target_out_dir/drm_framework_taihe_abc.abc"
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [
    ":drm_framework_taihe_abc"
  ]
}

group("drm_framework_taihe") {
  deps = [
    ":drm_framework_etc",
    ":drm_taihe",
  ]
}